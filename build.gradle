import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "java"
    id "maven-publish"
    id "wrapper"
}

sourceCompatibility = "1.7"

defaultTasks 'clean', 'licenseFormat', 'check', 'build'

group = "com.tealcube.minecraft.bukkit"
archivesBaseName = "mint"
version = "1.0.0-SNAPSHOT"

repositories {
    mavenCentral()
    maven {
        url "http://tealcube.com/nexus/content/groups/public/"
    }
    maven {
        url "http://nexus.theyeticave.net/content/repositories/pub_releases"
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
}

dependencies {
    compile "com.tealcube.minecraft.bukkit:facecore-all:1.0.0-SNAPSHOT"
    compile "net.milkbowl.vault:VaultAPI:1.5"
}

processResources {
    filter ReplaceTokens, tokens: [
            VERSION: version + "-" + (System.getenv("BUILD_NUMBER") ?: "0"),
            NAME   : project.name
    ]
}

license {
    header file('HEADER')
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        java(MavenPublication) {
            artifactId = "mint"
            from components.java
        }
    }
    repositories {
        maven {
            url "http://tealcube.com/nexus/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            name "TealCube"
            credentials {
                username System.getenv("nexusUsername") ?: nexusUsername
                password System.getenv("nexusPassword") ?: nexusPassword
            }
        }
    }
}
